buildscript {
    repositories {
        maven {
            jcenter()
            url "http://dl.bintray.com/jmoore/java-lib"
        }
    }
    dependencies {
        classpath 'com.mooregreatsoftware:gradle-defaults:4.0.0'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.4'
}

group = 'com.twcable.gradle'
description = 'Gradle plugin for working with Adobe CQ/AEM content packages'

apply plugin: 'groovy'

apply plugin: 'java-gradle-plugin'

repositories {
    maven {
        url "http://dl.bintray.com/twcable/aem"
    }

    // needed for the xmlunit SNAPSHOTS
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }

    mavenLocal()
}

dependencies {
    compile "com.twcable.gradle:gradle-plugin-cq-bundle:2.1.0"

    // can't use classifier because of a bug in Gradle's resolution only using group:artifact:version, not including classifier
    // testCompile group: "com.twcable.gradle", name: "gradle-plugin-cq-bundle-tests", version: bundlePlugin_version, ext: 'jar'

    // JCR file vault packaging
    compile "org.apache.jackrabbit.vault:org.apache.jackrabbit.vault:3.1.18"
    compile "org.apache.sling:org.apache.sling.jcr.jcr-wrapper:2.0.0"
    compile "org.apache.jackrabbit:jackrabbit-spi-commons:2.10.0", {
        exclude group: 'org.slf4j'
    }

    // @Nonnull @Nullable etc annotations
    compile "com.google.code.findbugs:jsr305:2.0.0"

    compile "commons-io:commons-io:2.4"

    testCompile "cglib:cglib-nodep:2.2"
    def xmlunit_version = "2.3.0"
    testCompile "org.xmlunit:xmlunit-core:${xmlunit_version}"
    testCompile "org.xmlunit:xmlunit-matchers:${xmlunit_version}"

    testCompile "org.eclipse.jetty:jetty-server:9.3.6.v20151106"

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile 'com.netflix.nebula:nebula-test:5.1.3'
    testCompile "org.gradle:gradle-tooling-api:${gradle.gradleVersion}" // hack to get the source
}

bintray {
    pkg {
        licenses = ['Apache-2.0']
        attributes = ['plat': ['gradle', 'aem', 'osgi', 'sling', 'cq']]

        //noinspection GroovyAssignabilityCheck
        version {
            attributes = ['gradle-plugin': ["com.twcable.gradle-plugin:${project.group}:${project.name}"]]
        }
    }
}

apply plugin: 'com.mooregreatsoftware.defaults'

defaults {
    compatibilityVersion = '1.8'

    id = 'twcable'

    orgName = 'Time Warner Cable'
    orgUrl = 'http://www.timewarnercable.com'

    bintrayRepo = 'aem'
    bintrayLabels = ['gradle', 'groovy', 'osgi']

    developers = [
        [id: 'jmoore', name: 'Jim Moore', email: 'moore.jim@gmail.com'],
    ]

    copyrightYears = '2014-2017'
}

wrapper {
    gradleVersion = "3.5"
}

javadoc {
    options.showFromPackage()
    options.addStringOption('Xdoclint:none', '-quiet')
}

// Hack around Oracle's breaking changes to JDK 1.8 Javadoc tool
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

groovydoc {
    link("https://jsr-305.googlecode.com/svn/trunk/javadoc/", "javax.annotation")
    link("http://docs.oracle.com/javase/8/docs/api/", 'java.')
    link("http://docs.groovy-lang.org/latest/html/api/", 'groovy.')
    link("https://twcable.github.io/gradle-plugin-cq-bundle/docs/groovydoc/", 'com.twcable.gradle', 'com.twcable.gradle.sling', 'com.twcable.gradle.sling.osgi')
}
